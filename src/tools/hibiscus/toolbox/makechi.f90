!!!=========+=========+=========+=========+=========+=========+=========+!
!!! HIBISCUS/toolbox/makechi @ iQIST                                     !
!!!                                                                      !
!!! This tool is used to postprocess the spin-spin correlation function  !
!!! (solver.schi.dat) which should be generated by the continuous time   !
!!! quantum Monte! Carlo impurity solver                                 !
!!! author  : Li Huang (at IOP/CAS & SPCLab/CAEP & UNIFR)                !
!!! version : v2016.02.13T                                               !
!!! status  : WARNING: IN TESTING STAGE, USE IT IN YOUR RISK             !
!!! comment : any question, please contact with lihuang.dmft@gmail.com   !
!!!=========+=========+=========+=========+=========+=========+=========+!

!!
!!
!! Introduction
!! ============
!!
!! The makechi code is often used to deal with the spin-spin correlation
!! function (in imaginary time). It can output the magnetic susceptibility
!! and effective magnetic moment. If the spin-spin correlation function is
!! in frequency space, this code can not be used directly.
!!
!! Usage
!! =====
!!
!! # ./mchi or build/mchi.x
!!
!! Input
!! =====
!!
!! solver.schi.dat (necessary)
!!
!! Output
!! ======
!!
!! N/A
!!
!! Documents
!! =========
!!
!! For more details, please see the on line reference manual.
!!
!!

  program makechi
     use constants, only : dp, zero, two, mystd, mytmp

     implicit none

! local control parameters
! number of bands
     integer  :: nband = 1

! number of time slices in [0, \beta]
     integer  :: ntime = 1024

! inversion of temperature
     real(dp) :: beta  = 10.00_dp

! local variables
! loop index
     integer  :: i
     integer  :: j

! status flag
     integer  :: istat

! logical file exist flag
     logical  :: exists

! magnetic susceptibility \chi_{loc}
     real(dp) :: xchi

! totally spin-spin correlation function: < Sz(0) Sz(\tau) >
     real(dp), allocatable :: schi(:)

! print program header
     write(mystd,'(2X,a)') 'HIBISCUS/toolbox/makechi'
     write(mystd,'(2X,a)') '>>> Making magnetic susceptibility from spin-spin correlation function'
     write(mystd,*) ! print blank line

     write(mystd,'(2X,a)') 'Version: 2016.02.13T '//'(built at '//__TIME__//" "//__DATE__//')'
     write(mystd,'(2X,a)') 'Develop: by li huang (at IOP/CAS & SPCLab/CAEP & UNIFR)'
     write(mystd,'(2X,a)') 'Support: lihuang.dmft@gmail.com'
     write(mystd,'(2X,a)') 'License: GNU General Public License version 3'
     write(mystd,*) ! print blank line

! setup necessary parameters
     write(mystd,'(2X,a)')   'Number of bands (default = 1):'
     write(mystd,'(2X,a,$)') '>>> '
     read (*,*) nband
     write(mystd,*)

     write(mystd,'(2X,a)')   'Number of time slices (default = 1024):'
     write(mystd,'(2X,a,$)') '>>> '
     read (*,*) ntime
     write(mystd,*)

     write(mystd,'(2X,a)')   'Inversion of temperature (default = 10.0):'
     write(mystd,'(2X,a,$)') '>>> '
     read (*,*) beta
     write(mystd,*)

! check the parameters
     call s_assert2( nband > 0 .and. nband < 8, 'wrong number of bands' )
     call s_assert2( ntime > 0, 'wrong number of time slices' )
     call s_assert2( beta > zero, 'wrong inversion of temperature' )

! allocate memory
     allocate(schi(ntime), stat=istat)
     if ( istat /= 0 ) then
         call s_print_error('makechi','can not allocate enough memory')
     endif ! back if ( istat /= 0 ) block

! initialize arrays
     schi = zero

! inquire data file
     inquire(file = 'solver.schi.dat', exist = exists)
     if ( exists .eqv. .false. ) then
         call s_print_error('makechi','file solver.schi.dat does not exist')
     endif ! back if ( exists .eqv. .false. ) block

! open data file: solver.schi.dat
     open(mytmp, file='solver.schi.dat', form='formatted', status='unknown')

! skip data block for orbital-resolved spin-spin correlation function
     do j=1,nband
         read(mytmp,*)
         do i=1,ntime
             read(mytmp,*)
         enddo ! over i={1,ntime} loop
         read(mytmp,*)
         read(mytmp,*)
     enddo ! over j={1,nband} loop

! read in spin-spin correlation function
     read(mytmp,*)
     do i=1,ntime
        read(mytmp,*) xchi, schi(i)
     enddo ! over i={1,ntime} loop

! close data file
     close(mytmp)

! rescale spin-spin correlation function
     schi = schi * real(nband)

! calculate local magnetic susceptibility
     xchi = zero
     do i=1,ntime-1
         xchi = xchi + ( schi(i) + schi(i+1) ) / two * ( beta / real(ntime - 1) )
     enddo ! over i={1,ntime-1} loop

! write out the calculated results
     write(mystd,'(2X,a,f12.6)') 'Magnetic susceptibility  :', xchi
     write(mystd,'(2X,a,f12.6)') 'Effective magnetic moment:', sqrt(xchi / beta)

! deallocate memory
     deallocate(schi)

  end program makechi
